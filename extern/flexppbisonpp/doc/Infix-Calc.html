<html lang="en">
<head>
<title>Bison 1.20</title>
<meta http-equiv="Content-Type" content="text/html">
<meta name=description content="Bison 1.20">
<meta name=generator content="makeinfo 4.1">
<link href="http://texinfo.org/" rel=generator-home>
</head>
<body>
<p>
Node:<a name="Infix%20Calc">Infix Calc</a>,
Next:<a rel=next href="Simple-Error-Recovery.html#Simple%20Error%20Recovery">Simple Error Recovery</a>,
Previous:<a rel=previous href="RPN-Calc.html#RPN%20Calc">RPN Calc</a>,
Up:<a rel=up href="Examples.html#Examples">Examples</a>
<hr><br>

<h3>Infix Notation Calculator: <code>calc</code></h3>

<p>We now modify rpcalc to handle infix operators instead of postfix.  Infix
notation involves the concept of operator precedence and the need for
parentheses nested to arbitrary depth.  Here is the Bison code for
<code>calc.y</code>, an infix desk-top calculator.

<br><pre>/* Infix notation calculator--calc */

%{
#define YYSTYPE double
#include &lt;math.h&gt;
%}

/* BISON Declarations */
%token NUM
%left '-' '+'
%left '*' '/'
%left NEG     /* negation--unary minus */
%right '^'    /* exponentiation        */

/* Grammar follows */
%%
input:    /* empty string */
        | input line
;

line:     '\n'
        | exp '\n'  { printf ("\t%.10g\n", $1); }
;

exp:      NUM                { $$ = $1;         }
        | exp '+' exp        { $$ = $1 + $3;    }
        | exp '-' exp        { $$ = $1 - $3;    }
        | exp '*' exp        { $$ = $1 * $3;    }
        | exp '/' exp        { $$ = $1 / $3;    }
        | '-' exp  %prec NEG { $$ = -$2;        }
        | exp '^' exp        { $$ = pow ($1, $3); }
        | '(' exp ')'        { $$ = $2;         }
;
%%
</pre>

<p>The functions <code>yylex</code>, <code>yyerror</code> and <code>main</code> can be the same
as before.

<p>There are two important new features shown in this code.

<p>In the second section (Bison declarations), <code>%left</code> declares token
types and says they are left-associative operators.  The declarations
<code>%left</code> and <code>%right</code> (right associativity) take the place of
<code>%token</code> which is used to declare a token type name without
associativity.  (These tokens are single-character literals, which
ordinarily don't need to be declared.  We declare them here to specify
the associativity.)

<p>Operator precedence is determined by the line ordering of the
declarations; the higher the line number of the declaration (lower on
the page or screen), the higher the precedence.  Hence, exponentiation
has the highest precedence, unary minus (<code>NEG</code>) is next, followed
by <code>*</code> and <code>/</code>, and so on.  See <a href="Precedence.html#Precedence">Operator Precedence</a>.

<p>The other important new feature is the <code>%prec</code> in the grammar section
for the unary minus operator.  The <code>%prec</code> simply instructs Bison that
the rule <code>| '-' exp</code> has the same precedence as <code>NEG</code>--in this
case the next-to-highest.  See <a href="Contextual-Precedence.html#Contextual%20Precedence">Context-Dependent Precedence</a>.

<p>Here is a sample run of <code>calc.y</code>:

<br><pre>% calc
4 + 4.5 - (34/(8*3+-3))
6.880952381
-56 + 2
-54
3 ^ 2
9
</pre>

</body></html>

